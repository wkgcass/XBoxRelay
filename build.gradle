plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10' apply false
    id 'org.beryx.jlink' version '2.26.0' apply false
}

group 'net.cassite'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'application'
    apply plugin: 'org.beryx.jlink'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jlink {
        options = ['--compress', '2', '--no-header-files', '--no-man-pages']
        launcher {
            name = 'app'
        }
        mergedModule {
            additive = true
            uses 'io.vertx.core.spi.VertxServiceProvider'
            uses 'io.vertx.core.spi.VerticleFactory'
            uses 'io.vertx.core.spi.JsonFactory'
            uses 'io.vertx.core.spi.launcher.CommandFactory'
        }
        forceMerge 'kotlin'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

project(":base") {
    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.31"
        api group: 'io.vproxy', name: 'vjson', version: '1.4.7'
        api group: 'io.vertx', name: 'vertx-core', version: '4.3.7'
        api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
        api group: 'io.projectreactor.tools', name: 'blockhound', version: '1.0.6.RELEASE'
    }
}

project(":agent") {
    dependencies {
        implementation project(":base")
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
        implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
    }
    task runAgent(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        main = "net.cassite.xboxrelay.agent.Main"
        var loglevel = System.getProperty("LogLevel")
        if (loglevel != null) {
            args = ["--log-level=" + loglevel]
        }
    }
}

project(":ui") {
    apply plugin: 'org.openjfx.javafxplugin'

    application {
        mainModule = 'net.cassite.xboxrelay.ui'
        mainClass = 'net.cassite.xboxrelay.ui.Main'
    }

    javafx {
        version = '17.0.1'
        modules = ['javafx.controls', 'javafx.swing', 'javafx.media']
    }

    dependencies {
        implementation project(":base")
        implementation group: 'io.vproxy', name: 'vfx', version: '1.3.0'
        implementation group: 'net.java.dev.jna', name: 'jna', version: '5.12.1'
        implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.12.1'
    }

    task runUI(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        main = "net.cassite.xboxrelay.ui.Main"
        var logLevel = System.getProperty("io.vproxy.vfx.logLevel")
        if (logLevel != null) {
            systemProperty("io.vproxy.vfx.logLevel", logLevel)
        }
    }
}
